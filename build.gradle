import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardOpenOption

buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = "https://repo.spongepowered.org/maven" }
        jcenter()
        mavenCentral()
        maven { url = 'https://jitpack.io' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
    //-------VERSIONING-------
    def versionPropsFile = file('version.properties')
    def Properties versionProps = new Properties()

    versionProps.load(new FileInputStream(versionPropsFile))

    def version_active      = versionProps['version.active'].toBoolean()
    def isSnapshot          = versionProps['version.snapshot'].toBoolean()
    def version_stage= versionProps['version.stage'].toString()
    def version_major       = versionProps['version.major'].toInteger()
    def version_minor       = versionProps['version.minor'].toInteger()
    def version_patch       = versionProps['version.patch'].toInteger()
    def version_build       = versionProps['version.build'].toInteger()


    if (isSnapshot) {
        if(version_active){
            version_build += 1
        }
        version = "${version_major}.${version_minor}.${version_patch}${version_stage}-snapshot#${version_build}"
        System.out.println("VERSIONING-SNAPSHOT")
    } else {
        version = "${version_major}.${version_minor}.${version_patch}${version_stage}"
        System.out.println("VERSIONING-RELEASE")

    }

    project.version = version
    //-------VERSIONING END-------
}
group mod_group

apply plugin: 'net.minecraftforge.gradle'


//------VERSION UPDATE IN FILE------
build{
    def versionPropsFile = file('version.properties')
    def Properties versionProps = new Properties()

    versionProps.load(new FileInputStream(versionPropsFile))

    def version_active = versionProps['version.active'].toBoolean()
    def isSnapshot     = versionProps['version.snapshot'].toBoolean()
    def version_build  = isSnapshot?
            project.version.toString().split("#")[1] : 0
    if(version_active){
        def propsFilePath = file('version.properties').absolutePath
        def lines = Files.readAllLines(Paths.get(propsFilePath))
        def updatedLines = []
        lines.each { line ->
            if (line.startsWith("version.build=")) {
                line = "version.build=$version_build"
            } else if (line.startsWith("version.snapshot=")) {
                line = "version.snapshot=true"
            }
            updatedLines.add(line)
        }
        Files.write(Paths.get(propsFilePath), updatedLines, StandardOpenOption.WRITE, StandardOpenOption.TRUNCATE_EXISTING)
        System.out.println("UPDATING BUILD VERSION")
    }
}
//------VERSION UPDATE FINISH------

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'
    apply plugin: 'net.minecraftforge.gradle'

    group mod_group
    version rootProject.version

    //encoding
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
    repositories {
        mavenCentral()
    }
    minecraft {
        mappings channel: 'snapshot', version: '20171003-1.12'
    }
    dependencies {
        minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'
        //global libs
        compileOnly project(":").fileTree(dir: 'libs', include: ['*.jar'])
        //local libs
//        api fileTree(dir: 'libs_impl', include: ['.jar'])

        //annotations
        compileOnly 'org.projectlombok:lombok:1.18.24'
        compileOnly 'org.jetbrains:annotations:23.0.0'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.jetbrains:annotations:23.0.0'
    }

    //replace placeholders from gradle.properties
    processResources {
        filesMatching('**/*.yml') {
            project.properties.each { propertyKey, propertyValue ->
                if (propertyValue instanceof String) {
                    filter {
                        it.replace("\${${propertyKey}}", propertyValue)
                    }
                }
            }
        }
        filesMatching('**/*.json') {
            project.properties.each { propertyKey, propertyValue ->
                if (propertyValue instanceof String) {
                    filter {
                        it.replace("\${${propertyKey}}", propertyValue)
                    }
                }
            }
        }
        filesMatching('**/*.info') {
            project.properties.each { propertyKey, propertyValue ->
                if (propertyValue instanceof String) {
                    filter {
                        it.replace("\${${propertyKey}}", propertyValue)
                    }
                }
            }
        }
    }
}
task copyOutputJars(type: Copy) {
    doFirst {
        // Delete existing JARs in the destination folder
        delete "$buildDir/OUTPUT"
    }
    // Ensure this task runs after the submodule's jar is built
    dependsOn ':gtw-mod-client:build'
    dependsOn ':gtw-mod-server:build'

    // Define the source and destination of the copy
    from "$projectDir/gtw-mod-client/build/libs", "$projectDir/gtw-mod-server/build/libs"
    into "$buildDir/OUTPUT"

    // Optionally, you can specify a pattern to copy only specific JARs
    include "${mod_name}-client-${rootProject.version}.jar"
    include "${mod_name}-server-${rootProject.version}.jar"
}

tasks.build.dependsOn(copyOutputJars)